# LFE Code


## 3 Hitting the Code

<img src="https://raw.github.com/lfe/docs/master/images/smash.jpg"
     style="float: right; padding-left: 1em;">It may not seem like it,
but we're off to a pretty fast start. If you had to do everything we've
done, manually, you'd have given up by now. Seriously.

Time to put the brakes on, though, 'cause you're gonna want to see this next
part in slow motion.


### 3.1 REPL Me Up!

Sure thing :-) Just do this from your new LFE project directory:

```bash
$ make repl-no-deps
```

This should give you something that looks like the following:

```
Erlang R16B (erts-5.10.1) [source] [smp:8:8] ...

LFE Shell V5.10.1 (abort with ^G)
>
```

We're not going to spend a lot of time in the REPL right now, but feel free to
take it for a spin. Try setting a variable:

```lisp
> (set my-list (lists:seq 1 6))
(1 2 3 4 5 6)
```

Here are some operations:

```lisp
> (* 2 (lists:sum my-list))
42
> (* 2 (lists:foldl (lambda (n acc) (+ n acc)) 0 my-list))
42
```

Turn that into a function:

```lisp
> (defun my-sum (start stop)
    (let ((my-list (lists:seq start stop)))
      (* 2 (lists:foldl
             (lambda (n acc)
               (+ n acc))
             0 my-list))))
my-sum
```

And try it out!

```lisp
> (my-sum 1 6)
42
```

Enough fancy stuff ... What about some *real* code? What does a **project**
look like?


### 3.2 Sample Code

Well, you've already seen one! Let's print it here for your viewing pleasure,
though. Here's the starter project you have, with just the one module:

```lisp
(defmodule my-test-lib
  (export all))

(defun my-adder (x y)
  (+ x y))
```

Let's go ahead and add our new ``my-sum`` function to that module in another
terminal window. Once you've done that, come back into the REPL and re-compile
the module:

```lisp
> (c "src/my-test-lib.lfe")
#(module my-test-lib)
> (my-test-lib:my-sum 1 6)
42
> (my-test-lib:my-sum 1 60)
3660
>
```

The other code that a generated LFE library project comes with is the unit
tests, which we've already seen:

```lisp
(defmodule unit-my-test-lib-tests
  (behaviour ltest-unit)
  (export all)
  (import
    (from ltest
      (check-failed-assert 2)
      (check-wrong-assert-exception 2))))

(include-lib "ltest/include/ltest-macros.lfe")

(deftest my-adder
  (is-equal 4 (: my-test-lib my-adder 2 2)))
```

Let's fill that out a little more with a few more tests. We can
also get rid of the imports we're not using. And we can update
the calls to use the new ``(mod:func ...)`` syntax:

```lisp
(defmodule unit-my-test-lib-tests
  (behaviour ltest-unit)
  (export all))

(include-lib "ltest/include/ltest-macros.lfe")

(deftest my-adder
  (is-equal 4 (my-test-lib:my-adder 2 2)))

(deftest my-sum
  (is-equal 42 (my-test-lib:my-sum 1 6))
  (is-equal 3660 (my-test-lib:my-sum 1 60)))

(deftest my-sum-zeros
  (is-equal 0 (my-test-lib:my-sum 0 0)))
```

Let's make sure our tests pass:

```bash
$ make check-unit
...
======================== EUnit ========================
module 'unit-my-test-lib-tests'
  my-adder ............................. [0.002 s] [ok]
  my-sum ......................................... [ok]
  my-sum-zeros ................................... [ok]
  Total module test time: 0.012 s
=======================================================
  All 3 tests passed.
```

Looking good :-)

Here's something a little more involved you may enjoy, from the examples in the
LFE source code:

```lisp
(defmodule messenger-back
 (export (print-result 0) (send-message 2)))

(defun print-result ()
  (receive
    ((tuple pid msg)
      (io:format "Received message: '~s'~n" (list msg))
      (io:format "Sending message to process ~p ...~n" (list pid))
      (! pid (tuple msg))
      (print-result))))

(defun send-message (calling-pid msg)
  (let ((spawned-pid (spawn 'messenger-back 'print-result ())))
    (! spawned-pid (tuple calling-pid msg))))
```

That bit of code demonstrates one of Erlang's core features in lovely Lisp
syntax: message passing. When loaded into the REPL, that example shows some
bidirectional message passing between the LFE shell and a spawned process.

Want to give it a try? Start by compiling the example module:

```lisp
> (c "deps/lfe/examples/messenger-back.lfe")
#(module messenger-back)
```

Next, let's send two messages to another Erlang process (in this case, we'll
send it to our REPL process, ``(self)``:

```lisp
> (messenger-back:send-message (self) "And what does it say now?")
#(<0.26.0> "And what does it say now?")
Received message: 'And what does it say now?'
Sending message to process <0.26.0> ...
> (messenger-back:send-message (self) "Mostly harmless.")
#(<0.26.0> "Mostly harmless.")
Received message: 'Mostly harmless.'
Sending message to process <0.26.0> ...
```

In the above calls, for each message sent we got a reply acknowledging the
message (because the example was coded like that). But what about the receiver
itself? What did it, our REPL process, see? We can flush the message
queue in the REPL to find out:

```lisp
> (c:flush)
Shell got {"And what does it say now?"}
Shell got {"Mostly harmless."}
ok
```

If you found this last bit interesting and want to step through a tutorial on
Erlang's light-weight threads in more detail, you may enjoy
[this tutorial](http://docs.lfe.io/tutorials/processes/1.html).


### Next Stop

We did promise a bit more information, so we're going to do that
next and then wrap up the quick start and point you in some
directions for your next LFE adventures ...

