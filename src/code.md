# Hitting the Code

<img src="https://raw.githubusercontent.com/lfe/docs/master/docs/current/images/smash.jpg"
     style="float: right; padding-left: 1em;">It may not seem like it,
but we're off to a pretty fast start. If you had to do everything we've
done, manually, you'd have given up by now. Seriously. Okay, maybe not.

Time to put the brakes on, though, 'cause you're gonna want to see this next
part in slow motion.


## REPL Me Up!

Sure thing :-) Just do this from your new LFE project directory:

```bash
$ rebar3 new repl
```

This should give you something that looks like the following:

```
Erlang/OTP 23 [erts-11.0.2] [source] [64-bit] [smp:12:12] [ds:12:12:10] [async-threads:1] [hipe] [dtrace]

   ..-~.~_~---..
  (      \\     )    |   A Lisp-2+ on the Erlang VM
  |`-.._/_\\_.-':    |   Type (help) for usage info.
  |         g |_ \   |
  |        n    | |  |   Docs: http://docs.lfe.io/
  |       a    / /   |   Source: http://github.com/rvirding/lfe
   \     l    |_/    |
    \   r     /      |   LFE v1.3-dev (abort with ^G)
     `-E___.-'

lfe>
```

We're not going to spend a lot of time in the REPL right now, but feel free to
take it for a spin. Try setting a variable:

```lisp
> (set my-list (lists:seq 1 6))
(1 2 3 4 5 6)
```

Here are some operations:

```lisp
> (* 2 (lists:sum my-list))
42
> (* 2 (lists:foldl (lambda (n acc) (+ n acc)) 0 my-list))
42
```

Turn that into a function:

```lisp
> (defun my-sum (start stop)
    (let ((my-list (lists:seq start stop)))
      (* 2 (lists:foldl
             (lambda (n acc)
               (+ n acc))
             0 my-list))))
my-sum
```

And try it out!

```lisp
> (my-sum 1 6)
42
```

Enough with the fancy REPL playu ... What about some *real* code? What does a **project**
look like?


## Sample Code

Well, you've already seen some! But here is the full, if minimal, module
generated by the LFE rebar3 plugin:

```lisp
(defmodule my-test-lib
  (export (my-fun 0)))

;;; -----------
;;; library API
;;; -----------

(defun my-fun ()
  'hello-world)
```

You'll note that the function we define has been exported via the `export`
form in the module definition. The number after the function is the arity
of that fucntion (Erlang views functions of the same name but different
arity as different functions, and LFE does the same).

In the REPL you will have access to this module and its one function. Try it
out:

```lisp
lfe> (my-test-lib:my-fun)
hello-world
```

Let's add to this module our new `my-sum` function from the REPL jam session
above. In another terminal window (or text editor pane) opem up the
`src/my-test-lib.lfe` file and past the `my-sum` function.

When you're done, the entire file should look like this:

```lisp
(defmodule my-test-lib
  (export (my-fun 0
          (my-sum 2))))

;;; -----------
;;; library API
;;; -----------

(defun my-fun ()
  'hello-world)

(defun my-sum (start stop)
  (let ((my-list (lists:seq start stop)))
    (* 2 (lists:foldl
            (lambda (n acc)
              (+ n acc))
            0 my-list))))
```

Then come back to the REPL sessions and compile the module with its new
addition:

```lisp
> (c "src/my-test-lib.lfe")
#(module my-test-lib)
> (my-test-lib:my-sum 1 6)
42
> (my-test-lib:my-sum 1 60)
3660
>
```

Here's something a little more involved you may enjoy, from the examples in the
LFE source code:

```lisp
(defun print-result ()
  (receive
    ((tuple pid msg)
      (io:format "Received message: '~s'~n" (list msg))
      (io:format "Sending message to process ~p ...~n" (list pid))
      (! pid (tuple msg))
      (print-result))))

(defun send-message (calling-pid msg)
  (let ((spawned-pid (spawn 'my-test-lib 'print-result ())))
    (! spawned-pid (tuple calling-pid msg))))
```

That bit of code demonstrates one of Erlang's core features in lovely Lisp
syntax: message passing. When loaded into the REPL, that example shows some
bidirectional message passing between the LFE shell and a spawned process.

Want to give it a try? Add these to your module, and don't forget to update
the `export` section of the module, too! (note that one function has an arity
of `0` and the other and arity of `2`).

When you're done, it should look like this:

```lisp
(defmodule my-test-lib
  (export (my-fun 0)
          (my-sum 2)
          (print-result 0)
          (send-message 2)))

;;; -----------
;;; library API
;;; -----------

(defun my-fun ()
  'hello-world)

(defun my-sum (start stop)
  (let ((my-list (lists:seq start stop)))
    (* 2 (lists:foldl
            (lambda (n acc)
              (+ n acc))
            0 my-list))))

(defun print-result ()
  (receive
    ((tuple pid msg)
      (io:format "Received message: '~s'~n" (list msg))
      (io:format "Sending message to process ~p ...~n" (list pid))
      (! pid (tuple msg))
      (print-result))))

(defun send-message (calling-pid msg)
  (let ((spawned-pid (spawn 'my-test-lib 'print-result ())))
    (! spawned-pid (tuple calling-pid msg))))
```

Now let's compile it and take it for a spin:

```lisp
> (c "src/my-test-lib.lfe")
#(module my-test-lib)
```

Next, let's send two messages to another Erlang process (in this case, we'll
send it to our REPL process, `(self)`:

```lisp
> (my-test-lib:send-message (self) "And what does it say now?")
#(<0.26.0> "And what does it say now?")
Received message: 'And what does it say now?'
Sending message to process <0.26.0> ...
> (my-test-lib:send-message (self) "Mostly harmless.")
#(<0.26.0> "Mostly harmless.")
Received message: 'Mostly harmless.'
Sending message to process <0.26.0> ...
```

In the above calls, for each message sent we got a reply acknowledging the
message (because the example was coded like that). But what about the receiver
itself? What did it, our REPL process, see? We can flush the message
queue in the REPL to find out.

What, what? Does each ...

Yup, every function in LFE (and Erlang, of course) has an inbox. You can see
how many messages a give process has by looking at the process' info:

```lisp
lfe> (erlang:process_info (self) 'message_queue_len)
#(message_queue_len 2)
```

And there you can see that our REPL process has two messages queued up in its
inbox. Let's take a look!

```lisp
> (c:flush)
Shell got {"And what does it say now?"}
Shell got {"Mostly harmless."}
ok
```

If you found this last bit interesting and want to step through a tutorial on
Erlang's light-weight threads in more detail, you may enjoy
[this tutorial](http://lfe.io/tutorial/concurrent/processes.html).

### Next Stop

We did promise a bit more information, so we're going to do that
next and then wrap up the quick start and point you in some
directions for your next LFE adventures ...
